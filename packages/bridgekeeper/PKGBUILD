# This file is part of Zulfiqar OS ( https://zulfiqar.os/ ).
# See COPYING for license details.

pkgname=bridgekeeper
pkgver=57.55c390c
pkgrel=7
pkgdesc='Scrape employee names from search engine LinkedIn profiles. Convert employee names to a specified username format.'
groups=('zulfiqar' 'zulfiqar-recon' 'zulfiqar-social')
arch=('any')
url='https://github.com/0xZDH/BridgeKeeper'
license=('custom:unknown')
depends=('python' 'python-requests' 'python-beautifulsoup4' 'python-lxml'
         'python-colorama')
makedepends=('git')
source=("$pkgname::git+https://github.com/0xZDH/BridgeKeeper.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir/usr/bin"
  install -dm 755 "$pkgdir/usr/share/$pkgname"

  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md

  rm README.md

  cp -a * "$pkgdir/usr/share/$pkgname/"

  cat > "$pkgdir/usr/bin/$pkgname" << EOF
#!/bin/sh
cd /usr/share/$pkgname
exec python $pkgname.py "\$@"
EOF

  chmod +x "$pkgdir/usr/bin/$pkgname"
}

