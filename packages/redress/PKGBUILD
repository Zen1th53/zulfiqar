# This file is part of Zulfiqar OS ( https://zulfiqar.os/ ).
# See COPYING for license details.

pkgname=redress
pkgver=v1.2.2.r0.gfe38d96
pkgrel=1
pkgdesc='A tool for analyzing stripped Go binaries.'
arch=('x86_64' 'aarch64')
groups=('zulfiqar' 'zulfiqar-binary' 'zulfiqar-reversing')
url='https://github.com/goretk/redress'
license=('AGPL')
makedepends=('git' 'go')
source=("git+https://github.com/goretk/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags="-s -w -X main.redressVersion=$pkgver" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

