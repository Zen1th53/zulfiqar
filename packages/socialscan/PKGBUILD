# This file is part of Zulfiqar OS ( https://zulfiqar.os/ ).
# See COPYING for license details.

pkgname=socialscan
pkgver=128.5ae42d0
pkgrel=5
pkgdesc='Check email address and username availability on online platforms.'
arch=('any')
groups=('zulfiqar' 'zulfiqar-recon')
url='https://github.com/iojw/socialscan'
license=('MIT')
depends=('python' 'python-aiohttp' 'python-colorama' 'python-tqdm')
makedepends=('git' 'python-setuptools')
source=("git+https://github.com/iojw/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

