# This file is part of Zulfiqar OS ( https://zulfiqar.os/ ).
# See COPYING for license details.

pkgname=wafw00f
pkgver=870.1899def
pkgrel=1
groups=('zulfiqar' 'zulfiqar-scanner' 'zulfiqar-webapp')
pkgdesc='Identify and fingerprint Web Application Firewall (WAF) products protecting a website.'
arch=('any')
url='https://github.com/EnableSecurity/wafw00f'
license=('GPL')
depends=('python' 'python-pluginbase' 'python-httpretty' 'python-urllib3'
         'python-beautifulsoup4' 'python-requests')
makedepends=('git' 'python-setuptools')
replaces=('waffit')
source=("git+https://github.com/EnableSecurity/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  python setup.py build
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build
}

