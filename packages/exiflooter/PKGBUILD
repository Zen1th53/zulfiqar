# This file is part of Zulfiqar OS ( https://zulfiqar.os/ ).
# See COPYING for license details.

pkgname=exiflooter
pkgver=39.0c9535f
pkgrel=3
pkgdesc='Find geolocation on all image urls and directories also integrates with OpenStreetMap.'
arch=('x86_64' 'aarch64')
groups=('zulfiqar' 'zulfiqar-forensic')
url='https://github.com/aydinnyunus/exifLooter'
license=('Apache')
depends=('perl-image-exiftool')
makedepends=('git' 'go')
source=("git+https://github.com/aydinnyunus/$pkgname.git")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd $pkgname

  GOPATH="$srcdir" go mod download
  GOPATH="$srcdir" go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-s -w" \
    -o $pkgname .
}

package() {
  cd $pkgname

  install -Dm 755 $pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}

